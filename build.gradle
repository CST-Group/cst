plugins {
	id 'java-library-distribution'
  	id 'jacoco'
  	id 'maven-publish'
}

group = 'com.github.CST-Group'

description = "CST"

sourceCompatibility = 1.8
targetCompatibility = 1.8
version = '1.2.0'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

configurations {
    extraLibs
}

dependencies {
   configurations.implementation.extendsFrom(configurations.extraLibs)
   
   api 'com.google.code.gson:gson:2.8.6'
   api 'net.openhft:compiler:2.3.0'  // Used by representation/owrl/CodeBuilder.java
   api group: 'org.json', name: 'json', version: '20160212'
   api group: 'org.opt4j', name: 'opt4j-core', version: '3.1'  // Used in GLAS
   api group: 'org.opt4j', name: 'opt4j-optimizers', version: '3.1'
   api group: 'org.opt4j', name: 'opt4j-viewer', version: '3.1' 
   api group: 'org.opt4j', name: 'opt4j-benchmarks', version: '3.1'
   api group: 'org.opt4j', name: 'opt4j-operators', version: '3.1'
   api group: 'org.opt4j', name: 'opt4j-satdecoding', version: '3.1'
   api group: 'org.opt4j', name: 'opt4j-tutorial', version: '3.1'
   api 'org.antlr:antlr4-runtime:4.5.3' // Used in OwrlBaseListener
   api 'com.github.masecla22:java-express:0.2.2'   
   testImplementation group: 'junit', name: 'junit', version: '4.13'
}

jar {
    from {
    	configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
    manifest {
      attributes(
        'Class-Path': configurations.runtimeClasspath.files.collect { it.getName() }.join(' ')    
      )
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs += ['-Xlint:unchecked']
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
    from {
    	configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
    }
    javadoc.options.addStringOption('Xdoclint:none', '-quiet')
}
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
    from {
    	configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
task uberJar(type: Jar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE    
    archiveClassifier = 'full'
    from sourceSets.main.output
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
}
artifacts
{
    archives javadocJar, sourcesJar, uberJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}
