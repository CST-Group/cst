plugins {
	id 'java-library-distribution'
  	id 'jacoco'
  	id 'maven'
}

group = 'com.github.CST-Group'

description = "CST is the Cognitive Systems Toolkit, a toolkit for the construction of Cognitive Systems and Cognitive Architectures"

sourceCompatibility = 1.8
targetCompatibility = 1.8
version = '0.5.2'

repositories {
	flatDir {
        dirs 'libs'
    }
    mavenCentral()
    maven { 
    	url "https://github.com/rosjava/rosjava_mvn_repo/raw/master"
    }
    maven {
      url "http://repository.springsource.com/maven/bundles/release"
    }
    maven {
      url "http://repository.springsource.com/maven/bundles/external"
    }
}

configurations {
    extraLibs
}

dependencies {
   extraLibs name:'jsoar-core'
   extraLibs name: 'jsoar-debugger'
   configurations.implementation.extendsFrom(configurations.extraLibs)
   
   api 'com.google.code.gson:gson:2.7'
   api 'org.slf4j:slf4j-api:1.7.5'
   api 'net.openhft:compiler:2.3.0'
   
   implementation group: 'org.jfree', name: 'jfreechart', version: '1.0.19'
   implementation group: 'org.json', name: 'json', version: '20160212'
   implementation group: 'net.sf.jung', name: 'jung-algorithms', version: '2.0.1'
   implementation group: 'net.sf.jung', name: 'jung-graph-impl', version: '2.0.1'
   implementation group: 'net.sf.jung', name: 'jung-visualization', version: '2.0.1'
   implementation group: 'org.opt4j', name: 'opt4j-core', version: '3.1'
   implementation group: 'org.opt4j', name: 'opt4j-optimizers', version: '3.1'
   implementation group: 'org.opt4j', name: 'opt4j-viewer', version: '3.1' 
   implementation 'org.antlr:antlr4-runtime:4.5.3'
   implementation 'ch.qos.logback:logback-classic:1.0.11' 
   implementation group: 'aopalliance', name: 'aopalliance', version: '1.0'
   implementation group: 'asm', name: 'asm', version: '3.1'
   implementation group: 'org.sonatype.sisu.inject', name: 'cglib', version: '2.2.1-v20090111'
   implementation group: 'com.google.inject', name: 'guice', version: '3.0'
   implementation group: 'com.google.inject.extensions', name: 'guice-multibindings', version: '3.0'
   implementation group: 'javax.inject', name: 'javax.inject', version: '1'
   implementation group: 'net.sf.jung', name: 'jung-api', version: '2.0.1'
   implementation group: 'org.opt4j', name: 'opt4j-benchmarks', version: '3.1'
   implementation group: 'org.opt4j', name: 'opt4j-operators', version: '3.1'
   implementation group: 'org.opt4j', name: 'opt4j-satdecoding', version: '3.1'
   implementation group: 'org.opt4j', name: 'opt4j-tutorial', version: '3.1'
   implementation group: 'org.ow2.sat4j', name: 'org.ow2.sat4j.core', version: '2.3.3'
   implementation 'commons-beanutils:commons-beanutils-core:1.8.0'
   implementation 'org.apache.commons:commons-math3:3.0'
   extraLibs 'org.ros.rosjava_core:rosjava:0.3.6'
   extraLibs 'org.ros.rosjava_bootstrap:message_generation:0.3.3'
   implementation 'dnsjava:dnsjava:2.1.9'
   extraLibs 'org.apache.commons:com.springsource.org.apache.commons.logging:1.1.1'
   extraLibs 'org.apache.commons:com.springsource.org.apache.commons.net:2.0.0'
   implementation 'com.google.guava:guava:28.0-jre'
   testImplementation 'com.1stleg:jnativehook:2.1.0'
   testImplementation group: 'junit', name: 'junit', version: '4.12'
   testImplementation 'org.ros.rosjava_messages:std_msgs:0.5.11'
   testImplementation 'org.ros.rosjava_messages:rosjava_test_msgs:0.3.0'
}

jar {
    from {
    	configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
    }
    manifest {
      attributes(
        'Class-Path': configurations.runtimeClasspath.files.collect { it.getName() }.join(' ')    
      )
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
    from {
    	configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
    from {
    	configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
task uberJar(type: Jar) {
    archiveClassifier = 'full'
    from sourceSets.main.output
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
}
artifacts
{
    archives javadocJar, sourcesJar, uberJar
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}